---
sidebar_position: 2
description: The Talo Godot plugin allows you to identify multiple aliases, authenticate and sync your players with Steamworks.
---

import { ScopeBadges } from '@site/src/components/ScopeBadges'

# Identifying a player

## Aliases

Players within your Talo account can have multiple aliases.
For example, a player could have Steam and Epic Games login credentials but both would be tied to them, allowing the player to use either to log in.

You should identify a player after they have authenticated and before you attempt to track any events, add leaderboard entries or do anything related directly to the player.

## Identifying

<ScopeBadges scope='players' read write />

You can identify a player using `Talo.players.identify()`. The code sample below shows you how you could identify a player using a UI element (this example is also available in the Playground scene):

```gdscript title="identify_button.gd"
extends Button

@export var service: String
@export var identifier: String

func _on_pressed():
	Talo.players.identify(service, identifier)
```

:::caution
You cannot use "Talo" for the `service` parameter as this is reserved for **Talo Player Authentication**.

If you are using Talo Player Authentication, `Talo.players.identify()` will be invoked automatically and the `Talo.players.identified` signal will also emit as normal.

Visit the [Player authentication docs](/docs/godot/player-authentication) to learn more about identifying players with authentication enabled.
:::

### Generating a mostly-unique identifier

You can easily create an identifier with `Talo.players.generate_identifier()`. This is useful for temporarily identifying players before you know who they are, and then merging them with an identified player later on.

### The "identified" signal

After a successful identification, the `Talo.players.identified` signal will emit, returning the identified player. This allows you to, for example, put the player's name on a label:

```gdscript title="player_name.gd"
extends Label

func _ready() -> void:
	Talo.players.identified.connect(_on_identified)

func _on_identified(player: TaloPlayer) -> void:
	text = player.get_prop("display_name")
```

### The "identification_started" and "identification_failed" signals

When `Talo.players.identify()` is called, the `Talo.players.identification_started` signal is emitted.

If identification fails, the `Talo.players.identification_failed` signal is emitted.

```gdscript
func _ready() -> void:
    Talo.players.identification_started.connect(func (): go_to_loading())
    Talo.players.identification_failed.connect(func (): go_to_login())
```

## Checking identification

Sometimes you might need to check if a player has been identified before. You can use `Talo.identity_check()` to verify this - it throws an error if a player hasn't been identified yet:

```gdscript
func do_stuff_if_authenticated() -> void:
	if Talo.identity_check() != OK:
		return

	# do stuff
```

## Clearing the identified player

You can clear the current player using `Talo.players.clear_identity()`. This will set `Talo.current_alias` and `Talo.current_player` to `null`. It will also clear any cached or pending data that identifies the player like the offline alias cache, pending events and continuity requests. For players using Talo authentication, it will also clear session data.

Once all the relevant data has been cleared, the `Talo.players.identity_cleared` signal will be emitted.

## Merging players

<ScopeBadges scope='players' read write />

Sometimes you might start tracking a player's actions before you know their true identity. For example, you could be tracking events with an "anonymous" identifier and then later on the same player chooses their username before submitting a leaderboard entry. Since both of these players need to be identified, two players will be created.

You can merge players using `Talo.players.merge()` by providing the IDs of both players. The merge process takes all the props, aliases, and associated data (events, leaderboard entries, saves, etc.) from **Player 2** and merges them into **Player 1**. This means that duplicate props in **Player 1** will be replaced by the ones from **Player 2**.

:::caution
There are a few limitations to merging players:

- **Player 2** cannot have a Talo Player Authentication alias or a Steam alias.
- If **Player 1** has a Talo Player Authentication or Steam alias, the merge must be initiated while identified as **Player 1** (i.e. `Talo.current_alias` must belong to Player 1). In this case, make sure your last `Talo.players.identify()` call before merging uses Player 1's alias.
- Both players cannot have overlapping alias services. For example, if both players have an alias with the service "username", the merging process will fail.
:::

You can provide the `post_merge_identity_service` option to automatically re-identify the player once merging is complete:

```gdscript
await Talo.players.identify("anonymous", Talo.players.generate_identifier())
var player1_id = Talo.current_player.id
await Talo.players.identify("username", "guyman")
var player2_id = Talo.current_player.id

print(Talo.current_alias.service) # "username"

var mergeOpts := Talo.players.MergeOptions.new()
mergeOpts.post_merge_identity_service = "anonymous" # go back to the anonymous alias
var merged_player := await Talo.players.merge(player1_id, player2_id, mergeOpts)

print(Talo.current_alias.service) # "anonymous"
```

In the example above, the two players created with `Talo.players.identify()` are merged. Before merging, the current alias service was **"username"** (because that was the most recently identified player). Setting the `post_merge_identity_service` option will invoke `Talo.players.identify()` with the **"anonymous"** alias.

## Steamworks integration

:::tip
You can enable this integration on the [integrations page](https://dashboard.trytalo.com/integrations).
:::

If you have the Steamworks integration enabled, Talo can sync a Steam player using the Steam User Auth API ([as described here](/docs/integrations/steamworks#authentication)). You can do this via the `Talo.players.identify_steam` function. Here's an example using [GodotSteam](https://godotsteam.com):

```gdscript
extends Node

var identity = "talo"

func _ready() -> void:
	Steam.steamInitEx()
	Steam.get_ticket_for_web_api.connect(_on_get_ticket_for_web_api)
	Steam.getAuthTicketForWebApi(identity)

func _on_get_ticket_for_web_api(_auth_ticket: int, _result: int, _ticket_size: int, ticket_buffer: Array) -> void:
	Talo.players.identify_steam(PackedByteArray(ticket_buffer).hex_encode(), identity)

func _process(_delta: float) -> void:
	Steam.run_callbacks()
```

The `identity` parameter is optional but strongly recommended as it ensures proper identification of the service verifying the ticket. It can be anything you like but must be the same as the `identity` passed to Steam when fetching the ticket.

### Steamworks player props

After successfully authenticating the player, several [props](/docs/godot/player-props) will automatically be created for them:

- `META_STEAMWORKS_VAC_BANNED` - "true" or "false"
- `META_STEAMWORKS_PUBLISHER_BANNED` - "true" or "false"
- `META_STEAMWORKS_OWNS_APP` - "true" or "false"
- `META_STEAMWORKS_OWNS_APP_PERMANENTLY` - "true" or "false"
- `META_STEAMWORKS_OWNS_APP_FROM_DATE` - ISO date e.g. "2025-11-08T17:30:00Z"
- `META_STEAMWORKS_PERSONA_NAME` - the player's current display name
- `META_STEAMWORKS_AVATAR_HASH` - e.g. "5bfdbd8cd4407d6cd37dfba06851b62a4856bb2d"

You can build an avatar URL by replacing **[AVATAR_HASH]** with the `META_STEAMWORKS_AVATAR_HASH` prop value in the following URL: `https://avatars.steamstatic.com/[AVATAR_HASH]_full.jpg`.

These props will be updated each time the player is identified using `Talo.players.identify_steam()`.

## Offline player cache

If the `cache_player_on_identify` setting is enabled (default `true`), Talo will store player data locally. If a player tries to identify while offline, Talo will try and use local data if it exists.

### Saving identifiers for future sessions

The offline player cache enables you to store and retrieve player identifiers. You can leverage this in situations where players choose their own identifiers and you need them in subsequent sessions:

```gdscript
func retrieve_identifier() -> String:
	var alias := TaloPlayerAlias.get_offline_alias()
	if alias != null:
		return alias.identifier
	return ""
```

This function will return the identifier (if one exists) or an empty string. If the return value of this function is not an empty string, you know its safe to call `Talo.players.identify()`.
